📦 files 🏠

🕊 🖼 🍇
	🖍🆕 pixels🍨🐚🍨🐚🔢🍆🍆

	🆕 🍼 pixels🍨🐚🍨🐚🔢🍆🍆 🍇🍉

	❓📍 row🔢 column🔢 ➡🔢 🍇
		↩ 🐽 🐽 pixels row ❗ column ❗
	🍉
🍉

🐇 🗃 🍇
	🐊 ↘🔸🔡

	🖍🆕 height🔢
	🖍🆕 width🔢
	🖍🆕 layers🍨🐚🖼🍆 ⬅ 🆕🍨🐚🖼🍆 ❗

	🆕 🍼 height🔢 🍼 width🔢 image🔡 🍇
		🍿 🆕🍨🐚🔢🍆 ❗ 🍆 ➡ 🖍🆕layer
		0 ➡ 🖍🆕row
		0 ➡ 🖍🆕column
		🔂 char 🎶 image ❗ 🍇
			🐻 🐽 layer row ❗ 🍺🔢 char 10 ❗ ❗

			column ⬅➕ 1
			↪ column 🙌 width 🍇
				0 ➡ 🖍column
				row ⬅➕ 1
				↪ row 🙌 height 🍇
					0 ➡ 🖍row
					🐻 layers 🆕🖼 layer ❗ ❗
					🐗 layer ❗
				🍉
				🐻 layer 🆕🍨🐚🔢🍆 ❗ ❗
			🍉
		🍉
		↪ 📏 layer ❓ ▶ 0 🤝 📏 🐽 layer 0 ❗ ❓ ▶ 0 🍇
			🐻 layers 🆕🖼 layer ❗ ❗
		🍉
	🍉

	❓📍 row🔢 column🔢 ➡🔢 🍇
		🔂 layer layers 🍇
			↪ ❎ 📍 layer row column ❓ 🙌 2 ❗ 🍇
				↩ 📍 layer row column ❓
			🍉
		🍉
		↩ 2
	🍉

	❗🔡 ➡🔡 🍇
		🆕🔠 ❗ ➡ sb
		🔂 row 🆕⏩ 0 height ❗ 🍇
			🔂 column 🆕⏩ 0 width ❗ 🍇
				📍 👇 row column ❓ ➡ pixel
				↪ pixel 🙌 1 🍇
					🐻 sb 🔤█🔤 ❗
				🍉
				🙅‍♀️↪ pixel 🙌 0 🍇
					🐻 sb 🔤░🔤 ❗
				🍉
				🙅‍♀️🍇
					🐻 sb 🔤 🔤 ❗
				🍉
			🍉
			🐻 sb 🔤❌n🔤 ❗
		🍉
		↩ 🔪 🔡 sb ❗ 0 width ✖ height ➕ height ➖ 1 ❗
	🍉
🍉

🏁 🍇
	🍺📇🐇📄 🔤./input.txt🔤 ❗ ➡ file
	🍺🔡 file ❗ ➡ text
	🆕🗃 6 25 text ❗ ➡ image

	😀 🔡 image ❗ ❗
🍉
