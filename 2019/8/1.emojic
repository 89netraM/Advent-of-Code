📦 files 🏠

🕊 🖼 🍇
	🐊 ↘🔸🔡

	🖍🆕 pixels🍨🐚🍨🐚🔢🍆🍆

	🆕 🍼 pixels🍨🐚🍨🐚🔢🍆🍆 🍇🍉

	❓🔄 digit🔢 ➡🔢 🍇
		0 ➡ 🖍🆕count

		🔂 row pixels 🍇
			🔂 pixel row 🍇
				↪ pixel 🙌 digit 🍇
					count ⬅➕ 1
				🍉
			🍉
		🍉

		↩ count
	🍉

	📗 Unnecessar(il)y (complex) toString method. 📗
	❗🔡 ➡🔡 🍇
		↩ 🆕🔡 🐰 pixels 🍇 row🍨🐚🔢🍆 ➡🔡 ↩ 🆕🔡 🐰 row 🍇 p🔢 ➡🔡 ↩ 🔡 p ❗ 🍉 ❗ 🔤🔤 ❗ 🍉 ❗ 🔤❌n🔤 ❗
	🍉
🍉

🐇 🗃 🍇
	🐊 ↘🔸🔡
	🐊 🔂🐚🖼🍆
	🐊 🐽🐚🖼🍆

	🖍🆕 height🔢
	🖍🆕 width🔢
	🖍🆕 layers🍨🐚🖼🍆 ⬅ 🆕🍨🐚🖼🍆 ❗

	🆕 🍼 height🔢 🍼 width🔢 image🔡 🍇
		🍿 🆕🍨🐚🔢🍆 ❗ 🍆 ➡ 🖍🆕layer
		0 ➡ 🖍🆕row
		0 ➡ 🖍🆕column
		🔂 char 🎶 image ❗ 🍇
			🐻 🐽 layer row ❗ 🍺🔢 char 10 ❗ ❗

			column ⬅➕ 1
			↪ column 🙌 width 🍇
				0 ➡ 🖍column
				row ⬅➕ 1
				↪ row 🙌 height 🍇
					0 ➡ 🖍row
					🐻 layers 🆕🖼 layer ❗ ❗
					🐗 layer ❗
				🍉
				🐻 layer 🆕🍨🐚🔢🍆 ❗ ❗
			🍉
		🍉
		↪ 📏 layer ❓ ▶ 0 🤝 📏 🐽 layer 0 ❗ ❓ ▶ 0 🍇
			🐻 layers 🆕🖼 layer ❗ ❗
		🍉
	🍉

	❗🐽 layer🔢 ➡🖼 🍇
		↩ 🐽 layers layer ❗
	🍉

	❓📏 ➡🔢 🍇
		↩ 📏 layers ❓
	🍉

	❗🍡 ➡🍡🐚🖼🍆 🍇
		↩ 🍡 layers ❗
	🍉

	📗 Unnecessary toString method. 📗
	❗🔡 ➡🔡 🍇
		🆕🔠 ❗ ➡ sb
		0 ➡ 🖍🆕count
		🔂 layer layers 🍇
			🍡 🔫 🔡 layer ❗ 🔤❌n🔤 ❗ ❗ ➡ lineIter
			count ⬅➕ 1
			🔡 count ❗ ➡ countStr
			8 ➕ 📐 countStr ❗ ➡ padding
			🐻 sb 🔤Layer 🧲countStr🧲: 🧲🔽 lineIter ❗🧲🔤 ❗
			↪ 🔽 lineIter ❓ 🍇
				🔁 🔽 lineIter ❓ 🍇
					🐻 sb 🔤❌n🔤 ❗
					🔂 _ 🆕⏩ 0 padding ❗ 🍇
						🐻 sb 🔤 🔤 ❗
					🍉
					🐻 sb 🔽 lineIter ❗ ❗
				🍉
			🍉
			🐻 sb 🔤❌n🔤 ❗
		🍉
		↩ 🔡 sb ❗
	🍉
🍉

🏁 🍇
	🍺📇🐇📄 🔤./input.txt🔤 ❗ ➡ file
	🍺🔡 file ❗ ➡ text
	🆕🗃 6 25 text ❗ ➡ image

	6 ✖ 25 ➕ 1 ➡ 🖍🆕min
	-1 ➡ 🖍🆕minIndex
	0 ➡ 🖍🆕index
	🔂 layer image 🍇
		🔄 layer 0 ❓ ➡ count
		↪ count ◀ min 🍇
			count ➡ 🖍min
			index ➡ 🖍minIndex
		🍉

		index ⬅➕ 1
	🍉

	↪ minIndex ▶🙌 0 🍇
		🐽 image minIndex ❗ ➡ layer
		😀 🔡 🔄 layer 1 ❓ ✖ 🔄 layer 2 ❓ ❗ ❗
	🍉
	🙅‍♀️🍇
		😀 🔤No layers in image 😟🔤 ❗
	🍉
🍉
