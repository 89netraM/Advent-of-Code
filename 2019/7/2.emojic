📦 files 🏠

🕊 🗄 🍇
	🐊 ↘🔸🔡

	🖍🆕 pc🔢 ⬅ 0
	🖍🆕 ops🍨🐚🔢🍆

	🆕 🍼 ops🍨🐚🔢🍆 🍇🍉
	🆕 🍼 pc🔢 🍼 ops🍨🐚🔢🍆 🍇🍉

	❗💻 move🔢 ➡🗄 🍇
		↩ 🆕🗄 pc ➕ move ops ❗
	🍉
	❗🖥 newPc🔢 ➡🗄 🍇
		↩ 🆕🗄 newPc ops ❗
	🍉

	❓💉 ➡🔢 🍇
		↩ 🐽 ops pc ❗ 🚮 100
	🍉

	❓📍 index🔢 ➡👌 🍇
		↩ 🤜 🤜 🐽 ops pc ❗ ➗ 🔢 🏂 10.0 💯 index ➕ 2 ❗ ❗ ❗ 🤛 🚮 10 🤛 🙌 0
	🍉

	❓📥 index🔢 ➡🔢 🍇
		🐽 ops pc ➕ index ➕ 1 ❗ ➡ param
		↪ 📍 👇 index ❓ 🍇
			↩ 🐽 ops param ❗
		🍉
		🙅‍♀️🍇
			↩ param
		🍉
	🍉
	❗📤 index🔢 value🔢 ➡🗄 🍇
		🐽 ops pc ➕ index ➕ 1 ❗ ➡ param
		ops ➡ 🖍🆕mutOps
		value ➡ 🐽 mutOps param ❗
		↩ 🆕🗄 pc mutOps ❗
	🍉

	❗🔡 ➡🔡 🍇
		🆕🔠 ❗ ➡ sb
		🔂 op ops 🍇
			🐻 sb 🔤🧲op🧲,🔤 ❗
		🍉
		↩ 🔪 🔡 sb ❗ 0 📐 sb ❗ ➖ 1 ❗
	🍉
🍉

🐇 🏃‍♀️ 🚧 🍇
	🖍🆕 state🗄

	🆕 🍼 state🗄 🍇
		⤴🆕 🔤Reached exit code (99)🔤 ❗
	🍉

	❗🗄 ➡🗄 🍇
		↩ state
	🍉
🍉

🐇 🏃‍♂️ 🍇
	🐇❗👷‍♀️ ➡🏃‍♂️ 🍇
		↩ 🆕🏃‍♂️ 📥🐇🏃‍♂️ ❗ 📤🐇🏃‍♂️ ❗ ❗
	🍉
	🐇❗📥 ➡🍇➡🔢🍉 🍇
		↩ 🍇🎍🥡 ➡🔢
			↪ 🔢 🆕🔡▶️👂🏼❗ 10 ❗ ➡ v 🍇
				↩ v
			🍉
			🙅‍♀️🍇
				🤯🐇💻 🔤Input only numbers🔤 ❗
				↩ 0
			🍉
		🍉
	🍉
	🐇❗📤 ➡🍇🔢🍉 🍇
		↩ 🍇🎍🥡 n🔢
			😀 🔡 n ❗ ❗
		🍉
	🍉

	🖍🆕 opFuns🍯🐚🍇🗄➡🗄🍉🍆

	🆕 in🍇➡🔢🍉 out🍇🔢🍉 🍇
		🍿
			🔤1🔤 ➡ 🍇🎍🥡 state🗄 ➡🗄
				📥 state 0 ❓ ➡ a
				📥 state 1 ❓ ➡ b
				↩ 💻 📤 state 2 a ➕ b ❗ 4 ❗
			🍉
			🔤2🔤 ➡ 🍇🎍🥡 state🗄 ➡🗄
				📥 state 0 ❓ ➡ a
				📥 state 1 ❓ ➡ b
				↩ 💻 📤 state 2 a ✖ b ❗ 4 ❗
			🍉
			🔤3🔤 ➡ 🍇🎍🥡 state🗄 ➡🗄
				↩ 💻 📤 state 0 ⁉ in ❗ ❗ 2 ❗
			🍉
			🔤4🔤 ➡ 🍇🎍🥡 state🗄 ➡🗄
				⁉ out 📥 state 0 ❓ ❗
				↩ 💻 state 2 ❗
			🍉
			🔤5🔤 ➡ 🍇🎍🥡 state🗄 ➡🗄
				↪ ❎ 📥 state 0 ❓ 🙌 0 ❗ 🍇
					↩ 🖥 state 📥 state 1 ❓ ❗
				🍉
				🙅‍♀️🍇
					↩ 💻 state 3 ❗
				🍉
			🍉
			🔤6🔤 ➡ 🍇🎍🥡 state🗄 ➡🗄
				↪ 📥 state 0 ❓ 🙌 0 🍇
					↩ 🖥 state 📥 state 1 ❓ ❗
				🍉
				🙅‍♀️🍇
					↩ 💻 state 3 ❗
				🍉
			🍉
			🔤7🔤 ➡ 🍇🎍🥡 state🗄 ➡🗄
				0 ➡ 🖍🆕b
				↪ 📥 state 0 ❓ ◀ 📥 state 1 ❓ 🍇
					1 ➡ 🖍b
				🍉
				↩ 💻 📤 state 2 b ❗ 4 ❗
			🍉
			🔤8🔤 ➡ 🍇🎍🥡 state🗄 ➡🗄
				0 ➡ 🖍🆕b
				↪ 📥 state 0 ❓ 🙌 📥 state 1 ❓ 🍇
					1 ➡ 🖍b
				🍉
				↩ 💻 📤 state 2 b ❗ 4 ❗
			🍉
		🍆 ➡ 🖍opFuns
	🍉

	❓🎭 op🔢 ➡🍇🗄➡🗄🍉 🍇
		↪ 🐽 opFuns 🔡 op ❗ ❗ ➡ opFun 🍇
			↩ opFun
		🍉
		🙅‍♀️🍇
			🤯🐇💻 🔤No such op (🧲op🧲)🔤 ❗
			↩ 🍇🎍🥡 s🗄 ➡🗄 ↩s 🍉
		🍉
	🍉

	❗🧟‍♀️ state🗄 ➡🗄 🍇
		↩ ⁉ 🎭 👇 💉 state ❓ ❓ state ❗
	🍉
	❗💨 state🗄 targetOp🔢 ➡🗄 🚧🏃‍♀️ 🍇
		state ➡ 🖍🆕 localState
		🔁 👍 🍇
			↪ 💉 localState ❓ 🙌 targetOp 🍇
				↩ localState
			🍉
			🙅‍♀️↪ 💉 localState ❓ 🙌 99 🍇
				🚨🆕🏃‍♀️ localState ❗
			🍉
			🙅‍♀️🍇
				🧟‍♀️ 👇 localState ❗ ➡ 🖍localState
			🍉
		🍉

		↩ localState
	🍉
	❗💨 state🗄 ➡🗄 🍇
		↩ 🍺💨 👇 state 99 ❗
	🍉
🍉

🐇 📿 🍇
	🐇❗🧂 elems🍨🐚🔢🍆 ➡🍨🐚🍨🐚🔢🍆🍆 🍇
		📏 elems ❓ ➡ count
		↪ count 🙌 1 🍇
			↩ 🍿 elems 🍆
		🍉
		🙅‍♀️🍇
			🆕🍨🐚🍨🐚🔢🍆🍆 ❗ ➡ 🖍🆕perms
			🔂 i 🆕⏩ 0 count ❗ 🍇
				🐽 elems i ❗ ➡ firstElem
				elems ➡ 🖍🆕innerElems
				🐨 innerElems i ❗
				🧂🐇📿 innerElems ❗ ➡ innerPerms
				🔂 perm innerPerms 🍇
					perm ➡ 🖍🆕innerPerm
					🐵 innerPerm 0 firstElem ❗
					🐻 perms innerPerm ❗
				🍉
			🍉
			↩ perms
		🍉
	🍉

	🖍🆕 value🔢 ⬅ 0
	🖍🆕 isFirst👌 ⬅ 👍
	🖍🆕 state🗄

	🆕 🍼 state🗄 🍇🍉

	❗📿 ids🍨🐚🔢🍆 ➡🔢 🍇
		📏 ids ❓ ➡ count
		🆕🍨🐚🗄🍆 state count ❗ ➡ 🖍🆕states
		🆕🏃‍♂️
			🍇🎍🥡 ➡🔢 ↩ value 🍉
			🍇🎍🥡 v🔢 ➡◼️ v ➡ 🖍value 🍉
		❗ ➡ runner

		0 ➡ 🖍value
		🔂 i 🆕⏩ 0 count ❗ 🍇
			🐽 ids i ❗ ➡ id
			👍 ➡ 🖍isFirst
			🍺💨
				🆕🏃‍♂️
					🍇🎍🥡 ➡🔢
						↪ isFirst 🍇
							👎 ➡ 🖍isFirst
							↩ id
						🍉
						🙅‍♀️🍇
							↩ value
						🍉
					🍉
					🍇🎍🥡 v🔢 🍉
				❗
				🐽 states i ❗
				4
			❗ ➡ temp
			🧟‍♀️ runner temp ❗ ➡ 🐽 states i ❗
		🍉

		👎 ➡ 🖍🆕hasExited
		🔁 ❎ hasExited ❗ 🍇
			🔂 i 🆕⏩ 0 count ❗ 🍇
				🐽 ids i ❗ ➡ id
				👍 ➡ 🖍isFirst
				🆗 temp 💨 runner 🐽 states i ❗ 4 ❗ 🍇
					🧟‍♀️ runner temp ❗ ➡ 🐽 states i ❗
				🍉
				🙅‍♀️ error 🍇
					👍 ➡ 🖍hasExited
				🍉
			🍉
		🍉

		↩ value
	🍉
🍉

🏁 🍇
	🍺📇🐇📄 🔤./input.txt🔤 ❗ ➡ file
	🍺🔡 file ❗ ➡ text

	🆕🗄 🐰 🔫 text 🔤,🔤 ❗ 🍇 s🔡 ➡🔢 ↩🍺🔢 s 10 ❗ 🍉 ❗ ❗ ➡ state
	🆕📿 state ❗ ➡ sequencer

	0 ➡ 🖍🆕maxOutput
	🔂 sequence 🧂🐇📿 🍿 5 6 7 8 9 🍆 ❗ 🍇
		📿 sequencer sequence ❗ ➡ output
		↪ output ▶ maxOutput 🍇
			output ➡ 🖍maxOutput
		🍉
	🍉

	😀 🔡 maxOutput ❗ ❗
🍉
