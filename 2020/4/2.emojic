📦 files 🏠

🏁 🍇
	🍺📇🐇📄 🔤./input.txt🔤 ❗ ➡ file
	🍺🔡 file ❗ ➡ text
	🔫 text 🔤❌n🔤 ❗ ➡ lines

	🆕🍨🐚🍨🐚🔡🍆🍆❗ ➡ 🖍🆕passports
	🆕🍨🐚🔡🍆❗ ➡ 🖍🆕currentPassport
	🔂 line lines 🍇
		↪ 📐 line ❗ ▶ 0 🍇
			🔂 field 🔫 🔧 line ❗ 🔤 🔤 ❗ 🍇
				🐻 currentPassport 🔧 field ❗ ❗
			🍉
		🍉
		🙅‍♀️↪ 📏 currentPassport ❓ ▶ 0 🍇
			🐻 passports currentPassport ❗
			🆕🍨🐚🔡🍆❗ ➡ 🖍currentPassport
		🍉
	🍉
	↪ 📏 currentPassport ❓ ▶ 0 🍇
		🐻 passports currentPassport ❗
	🍉

	0 ➡ 🖍🆕count
	🔂 passport passports 🍇
		👎 ➡ 🖍🆕byr
		👎 ➡ 🖍🆕iyr
		👎 ➡ 🖍🆕eyr
		👎 ➡ 🖍🆕hgt
		👎 ➡ 🖍🆕hcl
		👎 ➡ 🖍🆕ecl
		👎 ➡ 🖍🆕pid

		🔂 field passport 🍇
			📐 field ❗ ➡ length
			↪ 🎼 field 🔤byr🔤 ❗ 🤝 length 🙌 8 🍇
				🍺🔢 🔪 field 4 4 ❗ 10 ❗ ➡ y
				1920 ◀🙌 y 🤝 y ◀🙌2002 ➡ 🖍byr
			🍉
			🙅‍♀️↪ 🎼 field 🔤iyr🔤 ❗ 🤝 length 🙌 8 🍇
				🍺🔢 🔪 field 4 4 ❗ 10 ❗ ➡ y
				2010 ◀🙌 y 🤝 y ◀🙌 2020 ➡ 🖍iyr
			🍉
			🙅‍♀️↪ 🎼 field 🔤eyr🔤 ❗ 🤝 length 🙌 8 🍇
				🍺🔢 🔪 field 4 4 ❗ 10 ❗ ➡ y
				2020 ◀🙌 y 🤝 y ◀🙌 2030 ➡ 🖍eyr
			🍉
			🙅‍♀️↪ 🎼 field 🔤hgt🔤 ❗ 🍇
				↪ ⛳ field 🔤cm🔤 ❗ 🍇
					🍺🔢 🔪 field 4 length ➖ 6 ❗ 10 ❗ ➡ cm
					150 ◀🙌 cm 🤝 cm ◀🙌 193 ➡ 🖍hgt
				🍉
				🙅‍♀️↪ ⛳ field 🔤in🔤 ❗ 🍇
					🍺🔢 🔪 field 4 length ➖ 6 ❗ 10 ❗ ➡ in
					59 ◀🙌 in 🤝 in ◀🙌 76 ➡ 🖍hgt
				🍉
			🍉
			🙅‍♀️↪ 🎼 field 🔤hcl:#🔤 ❗ 🤝 length 🙌 11 🍇
				↪ 🔢 🔪 field 5 6 ❗ 16 ❗ ➡ _ 🍇
					👍 ➡ 🖍hcl
				🍉
				🙅‍♀️ 🍇
					👎 ➡ 🖍hcl
				🍉
			🍉
			🙅‍♀️↪ 🎼 field 🔤ecl🔤 ❗ 🤝 length 🙌 7 🍇
				🐦
					🍿 🔤amb🔤 🔤blu🔤 🔤brn🔤 🔤gry🔤 🔤grn🔤 🔤hzl🔤 🔤oth🔤 🍆
					🔪 field 4 3 ❗
				❓ ➡ 🖍ecl
			🍉
			🙅‍♀️↪ 🎼 field 🔤pid🔤 ❗ 🤝 length 🙌 13 🍇
				↪ 🔢 🔪 field 4 9 ❗ 10 ❗ ➡ _ 🍇
					👍 ➡ 🖍pid
				🍉
				🙅‍♀️ 🍇
					👎 ➡ 🖍pid
				🍉
			🍉
		🍉

		↪ byr 🤝 iyr 🤝 eyr 🤝 hgt 🤝 hcl 🤝 ecl 🤝 pid 🍇
			count ⬅➕ 1
		🍉
	🍉
	😀 🔡 count ❗ ❗
🍉
